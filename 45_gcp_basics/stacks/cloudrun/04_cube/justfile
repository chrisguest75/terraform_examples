#!/usr/bin/env just --justfile
# ^ A shebang isn't required, but allows a justfile to be executed
#   like a script, with `./justfile test`, for example.

set dotenv-load := true
export DOCKER_IMAGE_NAME:="04_cube"
export DOCKER_IMAGE_TAG:="latest"

# default lists actions
default:
  @just -f justfile --list

nix:
  #!/usr/bin/env bash
  set -xeufo pipefail
  nix develop --command zsh

clean:
  rm -rf ./dist || true
  rm -rf ./.parcel-cache || true

clean-all: clean
  rm -rf ./node_modules || true

install:
  #!/usr/bin/env bash
  set -eufo pipefail
  npm install


test:
  #!/usr/bin/env bash
  set -eufo pipefail
  npm run test

build:
  #!/usr/bin/env bash
  set -eufo pipefail
  npm run build

host:
  #!/usr/bin/env bash
  set -eufo pipefail
  mkdir -p ./dist/public
  cp -R ./public/ ./dist/public/
  npm run start  
  
watch:
  #!/usr/bin/env bash
  set -xeufo pipefail
  watchexec --exts ts npm run test

docker-build:
  #!/usr/bin/env bash
  set -xeufo pipefail

  npm run docker:build

docker-run: docker-build
  #!/usr/bin/env bash
  set -xeufo pipefail

  npm run docker:run

docker-run-amd64 image="${DOCKER_IMAGE_NAME}_amd64:latest":
  #!/usr/bin/env bash
  set -eufo pipefail
  docker run -it --platform linux/amd64 -p 8080:8080 --rm {{ image }}

docker-run-arm64 image="${DOCKER_IMAGE_NAME}_arm64:latest":
  #!/usr/bin/env bash
  set -eufo pipefail
  docker run -it --platform linux/arm64 -p 8080:8080 --rm {{ image }}

docker-shell:
  #!/usr/bin/env bash
  set -xeufo pipefail

  docker run --rm -it -p 8080:8080 --name ${DOCKER_IMAGE_NAME} ${DOCKER_IMAGE_NAME} bash

bake-build filter="":
  #!/usr/bin/env bash
  set -xeufo pipefail
  mkdir -p out || true
  export IMAGE_NAME=${DOCKER_IMAGE_NAME}
  export IMAGE_TAG=${DOCKER_IMAGE_TAG}
  docker buildx bake --progress=plain -f ./docker-bake.hcl --no-cache --metadata-file ./out/bake-metadata.json {{ filter }}

bake-build-push filter="":
  #!/usr/bin/env bash
  set -xeufo pipefail
  mkdir -p out || true

  export IMAGE_TAG=europe-west2-docker.pkg.dev/terraform-testing-467115/terraform-testing-cube/cube:latest
  docker buildx bake --progress=plain -f ./docker-bake.hcl --metadata-file ./out/bake-metadata.json --push push-cloudrun-image-amd64


