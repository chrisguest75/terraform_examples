#!/usr/bin/env just --justfile
# ^ A shebang isn't required, but allows a justfile to be executed
#   like a script, with `./justfile test`, for example.

set dotenv-load := true

# default lists actions
default:
  @just -f justfile --list

# check if AWS_PROFILE and AWS_REGION are set
checks:
  [ ! -z "${AWS_PROFILE-}" ] || (echo "AWS_PROFILE is not set"; exit 1)
  [ ! -z "${AWS_REGION-}" ] || (echo "AWS_REGION is not set"; exit 1)

clean:
  npm run clean

build: clean
  npm run build

test: 
  npm run test

create_npmrc: checks
  #!/usr/bin/env bash
  set -eufo pipefail
  export NPM_CONFIG_USERCONFIG=$(pwd)/.publish_npmrc
  echo "registry=https://${CODEARTIFACT_DOMAIN}-${CODEARTIFACT_DOMAIN_OWNER}.d.codeartifact.${AWS_REGION}.amazonaws.com/npm/${CODEARTIFACT_REPOSITORY}/" > ${NPM_CONFIG_USERCONFIG}
  echo "//${CODEARTIFACT_DOMAIN}-${CODEARTIFACT_DOMAIN_OWNER}.d.codeartifact.${AWS_REGION}.amazonaws.com/npm/${CODEARTIFACT_REPOSITORY}/:always-auth=true" >> ${NPM_CONFIG_USERCONFIG}
  echo "//${CODEARTIFACT_DOMAIN}-${CODEARTIFACT_DOMAIN_OWNER}.d.codeartifact.${AWS_REGION}.amazonaws.com/npm/${CODEARTIFACT_REPOSITORY}/:_authToken=\${CODEARTIFACT_AUTHTOKEN}" >> ${NPM_CONFIG_USERCONFIG}
  cat ${NPM_CONFIG_USERCONFIG}

publish: checks create_npmrc build 
  #!/usr/bin/env bash
  set -eufo pipefail

  echo "*************************************"
  echo "** Publish"
  echo "*************************************"
  
  export CODEARTIFACT_AUTHTOKEN=$(aws --profile $AWS_PROFILE --region $AWS_REGION codeartifact get-authorization-token --domain $CODEARTIFACT_DOMAIN --domain-owner $CODEARTIFACT_DOMAIN_OWNER --query authorizationToken --output text)
  export NPM_CONFIG_USERCONFIG=$(pwd)/.publish_npmrc
  cat ${NPM_CONFIG_USERCONFIG}

  npm publish

pull_tgz_package: checks create_npmrc
  #!/usr/bin/env bash
  set -eufo pipefail
  
  export CODEARTIFACT_AUTHTOKEN=$(aws --profile $AWS_PROFILE --region $AWS_REGION codeartifact get-authorization-token --domain $CODEARTIFACT_DOMAIN --domain-owner $CODEARTIFACT_DOMAIN_OWNER --query authorizationToken --output text)
  export NPM_CONFIG_USERCONFIG=$(pwd)/.publish_npmrc
  cat ${NPM_CONFIG_USERCONFIG}
  mkdir -p ./out

  npm view @chrisguest_codeartifact/01_test_package_npm dist.tarball      
  PACKAGE=$(npm pack --pack-destination ./out @chrisguest_codeartifact/01_test_package_npm)
  cd ./out
  tar -xvzf ${PACKAGE}


